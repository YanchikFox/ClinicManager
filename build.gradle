plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.14'
    id 'java'
}

group = 'com.clinicmanager'
version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.xerial:sqlite-jdbc:3.43.2.1'
    implementation 'org.slf4j:slf4j-api:2.0.13'
    runtimeOnly 'org.slf4j:slf4j-simple:2.0.13'
}

application {
    mainClass = 'com.clinicmanager.gui.MainFX'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

javafx {
    version = "21.0.1"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

tasks.register('initDatabase', JavaExec) {
    group = 'database'
    description = 'Initializes the local SQLite database using schema and seed data.'
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set('com.clinicmanager.tools.DatabaseSetup')
    args(project.findProperty('dbPath') ?: 'clinic.db')
}
